;;-*-Lisp-*-
(in-package goal)

(define-extern speedrun-practice-spawn (function none)) ;; defined at end of this file
(define-extern speedrun-practice-store-temp-checkpoint (function none)) ;; defined at end of this file
(define *practice-spawn-post-init* (the-as (function none) #f))

(define *last-real-continue-point* (the-as continue-point #f))

(define *tmp-continue-point* (new 'static 'continue-point
                             :name "tmp"
                             :level #f
                             :trans (new 'static 'vector :w 1.0)
                             :camera-trans (new 'static 'vector :w 1.0)
                             :quat (new 'static 'vector4h :data (new 'static 'array int16 4 0 #x4e76 0 -25885))
                             :camera-rot (new 'static 'array int16 9 #x6fe8 0 #x3e1a -6936 #x7328 #x30d7 -14302 -14299 #x64ae) ;; idk just stole this from existing continue-point
                             :on-goto #f
                             :vis-nick #f
                             :vehicle-type 0
                             :want-count 10
                             :want (new 'static 'inline-array level-buffer-state-small 10
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f)
                                    (new 'static 'level-buffer-state-small :name #f :display? #f))
                             :want-sound (new 'static 'array symbol 3 #f #f #f))
    )
(define *tmp-boost-count* 0.0)
(define *tmp-bbush-stored* (game-task none))

(defun bbush-task-started? ((arg0 game-task))
  (let ((v1-1 (-> *game-info* sub-task-list))
        (int-closed? #f)
        (res-open? #f))
    (dotimes (a1-0 (-> v1-1 length))
      (when (nonzero? a1-0)
        (let ((a2-3 (-> v1-1 a1-0)))
          (when (and (= (-> a2-3 task) arg0)
                     (logtest? (-> a2-3 flags) (game-task-node-flag bbush)))
            (cond 
              ((and (logtest? (-> a2-3 flags) (game-task-node-flag close-task))
                    (game-task-node-info-method-12 a2-3))
                ;; (format 0 "resolution open!~%")
                (true! res-open?))
              ((and (not (logtest? (-> a2-3 flags) (game-task-node-flag close-task)))
                    (logtest? (-> a2-3 flags) (game-task-node-flag closed)))
                ;; (format 0 "intro closed!~%")
                (true! int-closed?)))))))
    
    (and res-open? int-closed?)))

(defun bbush-task-restart! ((arg0 game-task))
  (let ((v1-1 (-> *game-info* sub-task-list)))
    (dotimes (a1-0 (-> v1-1 length))
      (when (nonzero? a1-0)
        (let ((a2-3 (-> v1-1 a1-0)))
          (when (and (= (-> a2-3 task) arg0) 
                     (logtest? (-> a2-3 flags) (game-task-node-flag bbush))
                     (not (logtest? (-> a2-3 flags) (game-task-node-flag close-task))))
            (open! a2-3 'event)
            (return #t)
            )
          )
        )
      )
    )
  #f
  )

(defun generic-post-init ()
  ;; try give vehicle
  (cond
    ((nonzero? (-> *tmp-continue-point* vehicle-type))
      (send-event *target* 'change-mode 'pilot #f (-> *tmp-continue-point* vehicle-type) #t))
    ((logtest? (-> *tmp-continue-point* flags) (continue-flags flut))
      (send-event *target* 'change-mode 'flut #f))
    )

  ;; restore old continue point
  (when *last-real-continue-point*
    (set! (-> *game-info* current-continue) *last-real-continue-point*))
  ;; copy exclusive tasks to re-open
  (when (nonzero? *tmp-bbush-stored*)
    (bbush-task-restart! *tmp-bbush-stored*))

  (none)
  )

(define-extern draw-torp-info (function none))
(define-extern draw-roll-challenge-info (function none))
(defun runs-every-frame ()
  (let ((torp (process-by-type-exact 'htorpedo *active-pool*)))
    (when torp (draw-torp-info)))

  (let ((mgr (process-by-type-exact 'task-manager-bbush-roll-count *active-pool*)))
    (when mgr (draw-roll-challenge-info)))

  ;; call custom checkpoint post-init function, if any
  (when (and *target*
              (progress-allowed?)
              (or (not *progress-process*) (gone? (-> *progress-process* 0)))
              (>= (- (-> *display* base-clock frame-counter) (-> *game-info* blackout-time)) (seconds 0.1))
              )
    (when *practice-spawn-post-init*
      (*practice-spawn-post-init*)
      (set! *practice-spawn-post-init* #f)
      )

    (when (cpad-hold? 0 r3)
      (cond
        ((cpad-pressed? 0 up)
          (format 0 "player tried to load tmp checkpoint~%")
          (speedrun-practice-spawn)
          )
        ((cpad-pressed? 0 down)
          (format 0 "player tried to save tmp checkpoint~%")
          (speedrun-practice-store-temp-checkpoint)
          )
        )
      )
    )

  ;; (if *show-input-display* 
  ;;   (input-display-on)
  ;;   (input-display-off)
  ;;   )
  
  (none)
  )

(defun speedrun-practice-temp-checkpoint ()
  (when (-> *tmp-continue-point* level)
    (cond
      ;; (*tmp-flut?* (set! *practice-spawn-post-init* get-on-flutflut))
      ;; (*tmp-zoomer?* (set! *practice-spawn-post-init* get-on-zoomer))
      (else (set! *practice-spawn-post-init* generic-post-init))
      )
    ;; commented out to prevent malding 
    ;; (if (cpad-hold? 0 r2)  ;; fully reset game if r2 held too
    ;;   (initialize! *game-info* 'game (the-as game-save #f) "default")
    ;;   )
    (when (not (string= (-> *game-info* current-continue name) "tmp"))
      (set! *last-real-continue-point* (-> *game-info* current-continue))
      )
    (set! (-> *game-info* current-continue) *tmp-continue-point*)
    (cheats-sound-play #f)
    (initialize! *game-info* 'dead (the-as game-save #f) (the-as string #f) (the-as resetter-spec #f))
    ;; (speedrun-reset-common-settings)
    )
  (none)
  )

(defun continue-point-copy! ((dst continue-point) (src continue-point))
  (set! (-> dst level) (or (-> src level) (-> *last-real-continue-point* level)))
  (set! (-> dst quat long) (-> src quat long))
  (set! (-> dst vis-nick) (-> src vis-nick))
  (dotimes (idx 10)
    ;; clear levels
    (set! (-> dst want idx name) #f)
    (set! (-> dst want idx display?) #f)
    (when (or (= (-> *level* level idx status) 'active)
              (= (-> *level* level idx status) 'loaded)
              (= (-> *level* level idx status) 'loading))
      (format 0 "adding ~S to tmp checkpoint, display: ~A~%" (-> *level* level idx name) (-> *level* level idx display?))
      (set! (-> dst want idx name) (-> *level* level idx name))
      (set! (-> dst want idx display?) (-> *level* level idx display?))

      (when (name= (-> *level* level idx name) (-> dst level))
        (format 0 "  forcing 'display for ~S since this is our continue point level~%" (-> *level* level idx name))
        (set! (-> dst want idx display?) 'display)
        )
      )
    )
  (dotimes (idx 3)
    (set! (-> dst want-sound idx)  (-> src want-sound idx))
    )
  (none)
  )

(defun camera-rot-copy! ((dst continue-point) (src matrix))
  (set! (-> dst camera-rot 0) (the int (* 32767.0 (-> src rvec x))))
  (set! (-> dst camera-rot 1) (the int (* 32767.0 (-> src rvec y))))
  (set! (-> dst camera-rot 2) (the int (* 32767.0 (-> src rvec z))))
  (set! (-> dst camera-rot 3) (the int (* 32767.0 (-> src uvec x))))
  (set! (-> dst camera-rot 4) (the int (* 32767.0 (-> src uvec y))))
  (set! (-> dst camera-rot 5) (the int (* 32767.0 (-> src uvec z))))
  (set! (-> dst camera-rot 6) (the int (* 32767.0 (-> src fvec x))))
  (set! (-> dst camera-rot 7) (the int (* 32767.0 (-> src fvec y))))
  (set! (-> dst camera-rot 8) (the int (* 32767.0 (-> src fvec z))))
  (none)
  )

(defun speedrun-practice-store-temp-checkpoint ()
  (case (-> *target* state)
    ;; only store temp checkpoint if in one of these states
    ((target-look-around target-stance target-wade-stance target-swim-stance target-pilot-stance target-flut-stance target-indax-stance)
      ;; assume current continue is good enough for the current location
      (continue-point-copy! *tmp-continue-point* (-> *game-info* current-continue))
      (vector-copy! (-> *tmp-continue-point* trans) (-> *target* root trans))
      (set! (-> *tmp-continue-point* quat x) (the int (* 32767.0 (-> *target* root quat x))))
      (set! (-> *tmp-continue-point* quat y) (the int (* 32767.0 (-> *target* root quat y))))
      (set! (-> *tmp-continue-point* quat z) (the int (* 32767.0 (-> *target* root quat z))))
      (set! (-> *tmp-continue-point* quat w) (the int (* 32767.0 (-> *target* root quat w))))
      (vector-copy! (-> *tmp-continue-point* camera-trans) (-> *camera-combiner* trans))
      (camera-rot-copy! *tmp-continue-point* (-> *camera-combiner* inv-camera-rot))
      
      ;; vehicle tracking stuff
      (set! *tmp-boost-count* 0.0)
      (set! (-> *tmp-continue-point* vehicle-type) 0)
      (when (focus-test? *target* pilot)
        (set! (-> *tmp-continue-point* vehicle-type) (the-as uint (-> *game-info* current-vehicle)))
        (let ((v (the vehicle (handle->process (-> *target* pilot vehicle)))))
          (when (and v (type-type? (-> v type) wvehicle))
            (set! *tmp-boost-count* (-> (the wvehicle v) turbo-supply)))))
          
      (cond 
        ((focus-test? *target* flut)
          (logior! (-> *tmp-continue-point* flags) (continue-flags flut)))
        (else
          (logclear! (-> *tmp-continue-point* flags) (continue-flags flut))))
      
      ;; copy exclusive task to re-open
      (set! *tmp-bbush-stored* (game-task none))
      (dotimes (idx (-> *game-info* task-node-exclusive length))
        (when (bbush-task-started? (the-as game-task (-> *game-info* task-node-exclusive idx)))
          (format 0 "found bbush started ~S~%" (game-task->string (the-as game-task (-> *game-info* task-node-exclusive idx))))
          (set! *tmp-bbush-stored* (the-as game-task (-> *game-info* task-node-exclusive idx)))))

      (cheats-sound-play #t)
      )
    )
  (none)
  )

(defun speedrun-practice-spawn ()
  ;; reset any previous post-target-init callback
  (set! *practice-spawn-post-init* #f)

  (speedrun-practice-temp-checkpoint)

  (none)
  )
